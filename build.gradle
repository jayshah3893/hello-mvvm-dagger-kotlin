// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
//    ext.kotlin_version = '1.3.20'
    ext {
        //app
        compileSdkVersion = 28
        minSdkVersion = 16
        targetSdkVersion = 28
        appcompatVersion = "1.1.0-alpha01"
        constraintLayoutVersion = "2.0.0-alpha3"
        moshiKotlinVersion = "1.5.0"
        dagger2Version = "2.13"
        archRoomVersion = "1.0.0"
        archLifecycleVersion = "1.1.0"
        retrofitVersion = "2.3.0"
        moshiConverterVersion = "2.3.0"
        rxJavaAdapterVersion = "2.3.0"
        rxAndroidVersion = "2.1.0"
        rxJava2Version = "2.1.0"
        navigationVersion = "1.0.0-alpha09"
        dataBindingVersion = "3.0.1"
        runnerVersion = "1.0.1"
        workVersion = "1.0.0-beta01"
        ktxVersion = "1.0.1"
        kotlinVersion = "1.3.20"
        ktlintVersion = "0.29.0"
        coroutinesVersion = "1.0.1"
        gradleVersion = "3.2.1"
        sonarqubeVersion = "2.6.2"
        recyclerViewVersion = "1.0.0"

        //test
        junitVersion = "4.12"

        //testImpl
        testRunnerVersion = "1.1.1"
        testEspressoVersion = "3.1.1"
    }

    repositories {
        google()
        jcenter()
        maven {
            url "http://dl.bintray.com/kotlin/kotlin-eap"
        }
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$gradleVersion"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "android.arch.navigation:navigation-safe-args-gradle-plugin:$navigationVersion"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
    id "com.diffplug.gradle.spotless" version "3.13.0"
    id "org.sonarqube" version "2.6.2"
}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url "http://dl.bintray.com/kotlin/kotlin-eap" }
    }
}

spotless {
    kotlin {
        target "**/*.kt"
        ktlint(ktlintVersion)
    }
}

apply plugin: "kotlin"
apply plugin: "org.sonarqube"

configurations {
    ktlint
}

dependencies {
    ktlint "com.github.shyiko:ktlint:0.23.1"
    // additional 3rd party ruleset(s) can be specified here
    // just add them to the classpath (e.g. ktlint 'groupId:artifactId:version') and
    // ktlint will pick them up
}

def outputDir = "${project.buildDir}/reports/ktlint/"
def inputFiles = project.fileTree(dir: "app", include: "src/**/*.kt")
def outputFile = "${outputDir}ktlint-checkstyle-report.xml"

print outputDir

task ktlint(type: JavaExec, group: "verification") {
    inputs.files(inputFiles)
    outputs.dir(outputDir)

    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args = [
            "--reporter=plain",
            "--reporter=checkstyle,output=${outputFile}",
            "app/src/**/*.kt"
    ]
}

check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "-F", "app/src/**/*.kt"
}

//task clean(type: Delete) {
//    delete rootProject.buildDir
//}
